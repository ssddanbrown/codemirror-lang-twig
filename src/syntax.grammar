@precedence {
  top,
  mid,
  lesser
}

@top Template { (InsertBlock | CodeBlock | CodeOpenBlock !mid | CodeCloseBlock !top | PlainText)* }

@skip { space | BlockComment }


InsertBlock { "{{" expression* "}}" }
CodeBlock { "{%" CodeBlockTag? expression* "%}"}
CodeOpenBlock { "{%" CodeOpenBlockTag expression* "%}"}
CodeCloseBlock { "{%" CodeCloseBlockTag "%}"}

commaSep<content> { "" | content ("," content?)* }

expression {
  Function |
  Identifier |
  ChainedIdentifier |
  String |
  Boolean |
  Number |
  Comparison
}

functionParam {
  Identifier |
  String |
  Boolean |
  Number
}

FunctionParamList {
  "(" commaSep<functionParam> ")"
}

Function { Identifier !top FunctionParamList}

@tokens {
  PlainText { ![{] PlainText? | "{" (@eof | ![{#%] PlainText?) }

  Identifier { $[a-zA-Z_\-0-9]+ }
  ChainedIdentifier { Identifier ("." Identifier)+ }

  CodeBlockTag { "set" | "extends" | "use" }
  CodeOpenBlockTag { "apply" | "for" | "if" | "block" | "autoescape" | "verbatim" | "macro" }
  CodeCloseBlockTag { "endapply" | "endfor" | "endif" | "endblock" | "endautoescape" | "endverbatim" | "endmacro" }

  Comparison {"and" | "or" | "not" | "==" | "!=" | "<" | ">" | ">=" | "<=" | "starts with" | "ends with" | "matches" | "in" | "not"}

  Boolean { "true" | "false" }
  String { ("'" (![\\'] | "\\" _)* "'"?) | ("\"" (![\\"] | "\\" _)* "\""?) }
  Number { '-'? int frac? }
  int  { '0' | $[1-9] @digit* }
  frac { '.' @digit+ }

  @precedence { PlainText, Boolean, Number, Comparison, ChainedIdentifier, CodeBlockTag, CodeOpenBlockTag, CodeCloseBlockTag, Identifier, space, BlockComment, String }

  BlockComment { "{#" blockCommentRest }
  blockCommentRest { ![#] blockCommentRest | "#" blockCommentAfterHash }
  blockCommentAfterHash { "}" | "#" blockCommentAfterHash | ![}#] blockCommentRest }

  space { $[ \t\n\r]+ }

  "{{" "}}" "{%" "%}"
  "(" ")"
}

@detectDelim
